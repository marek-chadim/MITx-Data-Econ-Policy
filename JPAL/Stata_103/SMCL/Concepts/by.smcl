{smcl}
{txt}{...}
{pstd}{c TLC}{hline 43}{c TRC}{break}
{bf}{...}
{c |}{view `""Stata 103.smcl""': Innovations for Poverty Action{space 12}}{c |}{break}
{c |}{view `""Stata 103.smcl""': The Abdul Latif Jameel Poverty Action Lab{space 1}}{c |}{break}
{c |}{view `""Stata 103.smcl""':{space 43}}{c |}{break}
{c |}{view `""Stata 103.smcl""': Staff Training{space 28}}{c |}{break}
{c |}{view `""Stata 103.smcl""': Stata 103{space 33}}{c |}{break}
{sf}{...}
{c BLC}{hline 43}{c BRC}

{pstd}{bf:{bf:by}}{break}

{pstd}{bf:{stata `"use "Raw/Stata 103", clear"'}}{p_end}

{pstd}In Stata, you often find yourself needing to repeat a command,
perhaps making minor adjustments from one call to another.
For example, suppose you want to examine summary statistics for variable {cmd:educ}
for all the different castes. You could try:

{pstd}{bf:{stata `"tabulate castecode, missing nolabel"'}}{p_end}

{pstd}{bf:{stata `"summarize educ if castecode == 1"'}}{p_end}
{pstd}{bf:{stata `"summarize educ if castecode == 2"'}}{p_end}
{pstd}{bf:{stata `"summarize educ if castecode == 3"'}}{p_end}
{pstd}{bf:{stata `"summarize educ if castecode == 4"'}}{p_end}
{pstd}{bf:{stata `"summarize educ if castecode == 5"'}}{p_end}
{pstd}{bf:{stata `"summarize educ if castecode == 6"'}}{p_end}
{pstd}{bf:{stata `"summarize educ if castecode == ."'}}{p_end}

{pstd}The similarity between the 7 lines implies a more efficient approach:
only 1 character out of 32 changes from one line to the next.
Imagine if you wanted to {cmd:summarize} by {cmd:castecode} not just {cmd:educ},
but also variables {cmd:sex}, {cmd:age}, and {cmd:literateyn}:
the code would catapult to 28 lines.
Not only is this hard to update when a variable name or something else changes,
more lines means greater risk of human error {hline 2}
especially when the coding is repetitive,
and it's hard to pay careful attention to each line.

{pstd}What's the solution?
We've seen one enormously flexible way to respond to this task:
{view `""SMCL/Concepts/For-Loops and Macros.smcl""':for-loops}.
Here's how we could use one here:

{pstd}{cmd}foreach i of numlist 1/6 . {{txt}{p_end}
{pmore}{cmd}summarize educ if castecode == `i'{txt}{p_end}
{pstd}{cmd}}{txt}{p_end}

{pstd}{stata `"run "Do/Concepts/by.do" 1"':Click here to execute.}

{pstd}That's a big step forward.
Yet, loops that run through each value of a variable are
so common that they have their own command: {helpb by}.
Above, we looped over each value of {cmd:castecode},
but instead we could use this {cmd:by} command:

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
sort castecode{break}
by castecode: summarize educ
{txt}{sf}{ul off}{...}

{pstd}{cmd:by} repeats Stata commands on subsets of the data, called "by-groups."
All observations of each by-group share the same value of the "by-variable"
(for example, {cmd:castecode} above).
The {cmd:by} command above first ran {cmd:summarize} for
all observations with {cmd:castecode == 1}, then
all those with {cmd:castecode == 2}, and so on.
Each of these was its own by-group.

{pstd}{cmd:by} is followed by a list of by-variables.
This list can include more than one variable.
For example, suppose you want summary statistics by caste and by sex,
contrasting not just OBC and SC, but also OBC male and OBC female, SC male and SC female, and so on:

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
sort castecode sex{break}
by castecode sex: summarize educ
{txt}{sf}{ul off}{...}

{pstd}You might be wondering why we needed to {cmd:sort} by {cmd:castecode sex} before using {cmd:by}.
{cmd:by} requires the dataset to be sorted by the by-variables.
You can do this before {cmd:by}, as we did above,
or at the same time by combining {cmd:by} and {cmd:sort} into one command,
named {cmd:bysort}:

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
bysort castecode sex: summarize educ
{txt}{sf}{ul off}{...}

{pstd}You'll get an error message if you try {cmd:by} without sorting by the by-variables,
either beforehand or through {cmd:bysort}. For example:

{pstd}{bf:{stata `"by sex: summarize educ"'}}{p_end}

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:Technical Tip!}}{c |}{p_end}
{pstd}{c LT}{hline 78}{c RT}{p_end}
{p2col:{c |} While {cmd:sort} only allows for sorting in ascending order, {helpb gsort}}{c |}{p_end}
{p2col:{c |} allows one to sort in both ascending and descending order.}{c |}{p_end}
{p2col:{c |} Use {cmd:gsort} as you use {cmd:sort}, but add a negative sign to any variable}{c |}{p_end}
{p2col:{c |} you wish to sort in descending order.}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} An example:}{c |}{p_end}
{p2col:{c |} {bf:{stata gsort surveyorid -surveydate1}}}{c |}{p_end}
{p2col:{c |} {bf:surveydate1} is now sorted in descending order within a given {bf:surveyorid}}{c |}{p_end}
{p2col:{c |} {bf:{stata browse surveyorid surveydate1}}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}

{marker n}{...}
{pstd}There's a nuance about {cmd:by} that can be extremely confusing if you aren't careful:
the behavior of {view `""SMCL/Concepts/Big N.smcl""':{bf:_N}} and {view `""SMCL/Concepts/Little n.smcl""':{bf:_n}}.

{pstd}We've seen that when you're {it:not} using {cmd:by},
{cmd:_N} is the number of observations, and
{cmd:_n} is the observation number.
For many commands, {cmd:_N} and {cmd:_n} continue to mean this when you use {cmd:by}.
Other commands don't allow {cmd:by} at all (aren't "byable").
For a third group of commands, {cmd:_N} and {cmd:_n} have the following meanings:

{pstd}{cmd:_N}: the number of observations {it:in the by-group}{break}
{cmd:_n}: the observation number {it:within the by-group}

{pstd}Here's an example:

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
sort sex{break}
generate datasetn = _n{break}
by sex: list datasetn if _n == 1
{txt}{sf}{ul off}{...}

{pstd}On the second line ({cmd:generate}), {cmd:_n} is the observation number within the dataset as a whole,
because there is no {cmd:by}; this is an ordinary use of {cmd:_n}.
In contrast, on the third line, {cmd:_n} is the observation number within groups of {cmd:sex}.
Thus, {cmd:_n == 1} for the first observation such that {cmd:sex == 1} and the first observation such that {cmd:sex == 2}.
The result was that on the third line, {cmd:_n == 1} even when {cmd:datasetn == 591}.

{pstd}Most commands for which you use {cmd:by} along with either {cmd:_N} or {cmd:_n} are of this third group,
including {cmd:list}, {cmd:generate}, and {cmd:drop}.

{pstd}Let's take a look at the data,
inspecting in particular where it switches from one by-group of {cmd:sex} to another:

{pstd}{bf:{stata `"by sex: generate byn = _n"'}}{p_end}
{pstd}{bf:{stata `"browse sex datasetn byn"'}}{p_end}

{pstd}We've touched on three subsets of commands. There's a rare fourth one:
commands that are byable but interpret {cmd:_N} and {cmd:_n} unpredictably.
Because of this unpredictability, you shouldn't use {cmd:_N} and {cmd:_n} with these commands.

{pstd}Here are examples of these four groups:

{pstd}{marker group1}{it:Group 1:} Not byable

{pstd}{cmd:display}

{pstd}{marker group2}{it:Group 2:}
When using {cmd:by}, {cmd:_N} and {cmd:_n} are with respect to the dataset as a whole

{pstd}{cmd:regress}

{pstd}{marker group3}{it:Group 3:}
When using {cmd:by}, {cmd:_N} and {cmd:_n} are with respect to the by-group

{pstd}{cmd}{...}
generate{break}
replace{break}
drop{break}
keep
{txt}{sf}{ul off}{...}

{pstd}{marker group4}{it:Group 4:}
When using {cmd:by}, the values of {cmd:_N} and {cmd:_n} are unpredictable

{pstd}{cmd:egen}

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:Technical Tip!}}{c |}{p_end}
{pstd}{c LT}{hline 78}{c RT}{p_end}
{p2col:{c |} How can you tell which of the four groups a command belongs to?}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} First of all, most commands for which you use {cmd:by} along with either {cmd:_N} or {cmd:_n}}{c |}{p_end}
{p2col:{c |} are {view `""SMCL/Concepts/by.smcl"##group3"':group 3}. The most important of these are:}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} {cmd}generate}{c |}{p_end}
{p2col:{c |} replace}{c |}{p_end}
{p2col:{c |} drop}{c |}{p_end}
{p2col:{c |} keep}{c |}{p_end}
{p2col:{c |} list}{c |}{p_end}
{p2col:{c |} assert{txt}}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} See {view `""SMCL/Concepts/Group 3 commands.smcl""':here} for a fuller list of group 3 commands.}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} You can also use these guidelines to determine the group to which a command}{c |}{p_end}
{p2col:{c |} belongs:}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} 1. Does the command allow the {cmd:if} qualifier?}{c |}{p_end}
{p2col:{c |} - If {bf:no}: the command is group 1; skip remaining questions.}{c |}{p_end}
{p2col:{c |} - If {bf:yes}: the command is group 2, 3, or 4.}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} 2. Is the command a "built-in command"? To find out, use {helpb which}:}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} {bf:{stata which generate}}}{c |}{p_end}
{p2col:{c |} {bf:{stata which regress}}}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} {cmd:generate} is a built-in command, but {cmd:regress} isn't. Although written by}{c |}{p_end}
{p2col:{c |} StataCorp, {cmd:regress} is written in Stata, unlike {cmd:generate}. (Yes, many Stata}{c |}{p_end}
{p2col:{c |} commands are themselves written in Stata!)}{c |}{p_end}
{p2col:{c |} - If {bf:yes} (built-in): the command is group 3; skip remaining question.}{c |}{p_end}
{p2col:{c |} - If {bf:no} (not built-in): the command is group 2 or group 4.}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} 3. Is the command {cmd:egen} or user-written?}{c |}{p_end}
{p2col:{c |} - If {bf:no}: the command is group 2.}{c |}{p_end}
{p2col:{c |} - If {bf:yes}: the command is group 4.}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}

{hline}{marker q4}

{pstd}{bf:Problem Set Question 4}

{hline}

{pstd}{bf:{stata `"use "Raw/Stata 103", clear"'}}{p_end}

{pstd}Create a variable named {cmd:sexn} that is the number of respondents
of a given sex. For example, if {cmd:sex = Male}, {cmd:sexn} should equal {cmd:590}.

{pstd}What group(s) are the command(s) you used?
{txt}{sf}{ul off}{...}

{hline}{marker q5}

{pstd}{bf:Problem Set Question 5}

{hline}

{pstd}{bf:{stata `"use "Raw/Stata 103", clear"'}}{p_end}

{pstd}Create a variable named {cmd:treatment} that is {cmd:1} or {cmd:0}.
Use {cmd:by}, {cmd:_N}, and {cmd:_n}
to ensure that treatment is balanced with respect to {cmd:sex}
by assigning {cmd:treatment = 1} to the first half of males
and the first half of females
and {cmd:treatment = 0} to the second half of males and the second half of females.

{pstd}To check that you got it right:

{pstd}{bf:{stata `"bysort sex: tabulate treatment"'}}{p_end}

{pstd}Or:

{pstd}{bf:{stata `"tabulate sex treatment"'}}{p_end}
{pstd}{txt}{sf}{ul off}{...}

{hline}

{pstd}You've now achieved the "stratified" part of stratified randomization.
Congratulations!

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:Technical Tip!}}{c |}{p_end}
{pstd}{c LT}{hline 78}{c RT}{p_end}
{p2col:{c |} Two Stata FAQs describe how to use {cmd:by}, {cmd:_N}, and {cmd:_n} to detect duplicates:}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} {browse "http://www.stata.com/support/faqs/data-management/duplicate-observations/":How do I identify duplicate observations in my data?}}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} {browse "http://www.stata.com/support/faqs/data-management/number-of-distinct-observations/":How do I compute the number of distinct observations?}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}

{marker by_paren}{...}
{pstd}We will now touch on a variant of {cmd:by}.
How can we use {cmd:by} to create a variable
that is the maximum age within the respondent's sex?
For example, if the oldest female is {cmd:103},
then this variable would equal {cmd:103} for all female respondents.

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
bysort sex (age): generate maxage = age[_N]
{txt}{sf}{ul off}{...}

{pstd}{cmd:generate} is a {view `""SMCL/Concepts/by.smcl"##group3"':group 3} command,
so {cmd:_N} is the number of observations of the by-group of {cmd:sex}.
Thus, this command creates a variable named {cmd:maxage} that is the {cmd:age} of the last observation in the by-group.
You might be wondering
why this would necessarily be the {it:maximum} {cmd:age} in the by-group;
this is where {cmd:(age)} comes in.
{cmd:(age)}, after the by-variable {cmd:sex}, ensures that
after the dataset is sorted by the by-variable (i.e., {cmd:sex}), it is then sorted by {cmd:age}.
The dataset will then be sorted by {cmd:sex age}, which means that {cmd:age[_N]} will be both
the {cmd:age} of the last observation in the by-group of {cmd:sex}
and the maximum {cmd:age} in the by-group.
The by-groups are still of {cmd:sex} and not {cmd:sex age} {hline 2}
{cmd:age} is not a by-variable {hline 2}
but {cmd:(age)} has the effect that the by-groups of {cmd:sex} will be
internally sorted by {cmd:age}.

{pstd}{bf:{stata `"tabulate sex maxage"'}}{p_end}
{pstd}{bf:{stata `"by sex: summarize age"'}}{p_end}

{pstd}We can use this feature of {cmd:by} for duplicates cleaning.
Recall that {view `""SMCL/Concepts/if.smcl"##loop"':before},
we determined the differences between {cmd:hhid "1802011"} duplicates as follows:

{pstd}{cmd}sort hhid{txt}{p_end}
{pstd}{cmd}foreach var of varlist _all {{txt}{p_end}
{pmore}{cmd}if `var'[597] != `var'[598] {{txt}{p_end}
{pmore2}{cmd}display "The two observations of 1802011 differ on `var'."{txt}{p_end}
{pmore}{cmd}}{txt}{p_end}
{pstd}{cmd}}{txt}{p_end}

{pstd}{stata `"run "Do/Concepts/by.do" 2"':Click here to execute.}

{pstd}This worked, but there is a downside to this approach: it depends on two observation numbers,
{cmd:597} and {cmd:598},
so if the sort order is changed, {cmd:hhid} is modified,
an observation is added or dropped, and so on,
the code could very well stop working.
If there's any code we've used so far that's likely to result in a bug, this is it.

{pstd}Instead, we can achieve the same result using {cmd:by}:

{pstd}{cmd}foreach var of varlist _all {{txt}{p_end}
{pmore}{cmd}bysort hhid (`var'): generate unequal = `var'[1] != `var'[_N]{txt}{p_end}
{pmore}{cmd}list hhid `var' if unequal == 1{txt}{p_end}
{pmore}{cmd}drop unequal{txt}{p_end}
{pstd}{cmd}}{txt}{p_end}

{pstd}{stata `"run "Do/Concepts/by.do" 3"':Click here to execute.}

{pstd}Again, we're looping over all variables. For each variable, we create a variable named {cmd:unequal}
that is {cmd:1} if an observation differs from another {cmd:hhid} duplicate on the variable, and {cmd:0} otherwise.
We're executing {cmd:generate} (again, a {view `""SMCL/Concepts/by.smcl"##group3"':group 3} command)
for each by-group of {cmd:hhid}, which is sorted by {cmd:`var'}.
If a value of {cmd:hhid} has no duplicates, then its by-group will have exactly {cmd:1} observation, so {cmd:_N == 1},
so {cmd:`var'[1] == `var'[_N]}, so {cmd:unequal = 0}. If instead a value of {cmd:hhid} does have duplicates (for example, {cmd:"1802011"}),
then {cmd:unequal} equals {cmd:1} if the first value of {cmd:`var'} (for numeric variables: the minimum; for strings: the first alphabetically) differs
from the last value of {cmd:`var'} (for numeric variables: the maximum; for strings: the last alphabetically), and {cmd:0} otherwise.

{hline}{marker q6}

{pstd}{bf:Problem Set Question 6}

{hline}

{pstd}{bf:{stata `"use "Raw/Stata 103", clear"'}}{p_end}

{pstd}Using the {cmd:by} {it:varlist1} {cmd:(}{it:varlist2}{cmd:)} syntax,
create a variable named {cmd:over30} that is
{cmd:1} if any member of a respondent's {cmd:castename} is older than {cmd:30},
and {cmd:0} otherwise.
{txt}{sf}{ul off}{...}

{hline}

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {bf:More on by}}{c |}{p_end}
{pstd}{c LT}{hline 78}{c RT}{p_end}
{p2col:{c |} For more on by, see the terrific Stata Journal article {browse "http://www.stata-journal.com/sjpdf.html?articlenum=pr0004":{it:Speaking Stata: How}}}{c |}{p_end}
{p2col:{c |} {browse "http://www.stata-journal.com/sjpdf.html?articlenum=pr0004":{it:to move step {bf:by} step}}. It's really worth the short read to learn more about}{c |}{p_end}
{p2col:{c |} this key command.}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}


{hline}

{pstd}Problem Set: {view `""SMCL/Problem sets/Concepts/by - problem set.smcl""':{bf:by}}{break}

{pstd}Next: {view `""SMCL/Concepts/egen.smcl""':{bf:egen}}{break}
Previous: {view `""SMCL/Concepts/Little n.smcl""':{bf:_n}}{break}
{view `""Stata 103.smcl""':Stata 103 Start}{break}
