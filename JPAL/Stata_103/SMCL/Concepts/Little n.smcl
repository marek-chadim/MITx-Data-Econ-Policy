{smcl}
{txt}{...}
{pstd}{c TLC}{hline 43}{c TRC}{break}
{bf}{...}
{c |}{view `""Stata 103.smcl""': Innovations for Poverty Action{space 12}}{c |}{break}
{c |}{view `""Stata 103.smcl""': The Abdul Latif Jameel Poverty Action Lab{space 1}}{c |}{break}
{c |}{view `""Stata 103.smcl""':{space 43}}{c |}{break}
{c |}{view `""Stata 103.smcl""': Staff Training{space 28}}{c |}{break}
{c |}{view `""Stata 103.smcl""': Stata 103{space 33}}{c |}{break}
{sf}{...}
{c BLC}{hline 43}{c BRC}

{pstd}{bf:{bf:_n}}{break}

{pstd}{bf:{stata `"use "Raw/Stata 103", clear"'}}{p_end}

{pstd}{helpb _n} is the observation number according to the current sort order.
For example:

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
{cmd:* Variable order will equal the observation number.}{break}
generate order = _n
{txt}{sf}{ul off}{...}

{pstd}{bf:{stata `"browse order"'}}{p_end}
{pstd}{cmd:* Change the sort order.}{p_end}
{pstd}{bf:{stata `"sort sex"'}}{p_end}
{pstd}{cmd:* Since the sort order has changed, order is jumbled.}{p_end}
{pstd}{bf:{stata `"browse order"'}}{p_end}
{pstd}{cmd:* Restore the initial sort order.}{p_end}
{pstd}{bf:{stata `"sort order"'}}{p_end}
{pstd}{bf:{stata `"browse order"'}}{p_end}

{pstd}{helpb _n} changes every time you sort,
so you should never use it in combination with {cmd:generate}, {cmd:drop}, or other
commands without explicitly sorting by a unique variable first.
Note that the variable for sort has to be a unique ID – if you sort by a non-unique variable (say household ID),
the individual observations will be randomized within it.
When you then proceed to, for example, drop the 1st observation in each household,
the observation dropped will be different each time you re-run the dofile.
This is a mistake that can lead to significant issues with your cleaning down the line.

{pstd}{cmd:_n} is different from {view `""SMCL/Concepts/Big N.smcl""':{bf:_N}}:
while {cmd:_n} is the observation number
and therefore changes from observation to observation,
{cmd:_N} is constant as long as the number of observations stays the same.
They are similar in the sense that both are built-in system variables.
They do not need to be created, unlike the {view `""SMCL/Concepts/For-Loops and Macros.smcl""':locals} you've
seen previously, rather, they are automatically generated and updated by Stata.

{pstd}We can use {cmd:_n} to refer to the previous observation, the one two previous,
the next observation, and so on.
For example, the following code creates a variable
that equals the {cmd:hhid} of the previous observation:

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
generate previousid = hhid[_n - 1]
{txt}{sf}{ul off}{...}

{pstd}{bf:{stata `"browse hhid previousid"'}}{p_end}

{pstd}Note the value of {cmd:previousid} in the first observation,
for which {cmd:_n == 1}.
{cmd:previousid[1]} was defined as {cmd:hhid[0]},
which doesn't exist, so
{cmd:previousid[1]} was set to missing.

{pstd}We can use {cmd:_n}
as part of our favorite check:
that of the unique ID.
The code below creates a dummy variable named {cmd:iddup}:

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
sort hhid{break}
generate iddup = hhid == hhid[_n - 1]
{txt}{sf}{ul off}{...}

{hline}{marker q2}

{pstd}{bf:Problem Set Question 2}

{hline}

{pstd}Can you interpret the logical expression {cmd:hhid == hhid[_n - 1]}?
{txt}{sf}{ul off}{...}

{hline}

{pstd}How would {cmd:iddup} perform as an indicator
that an observation is a duplicate on {cmd:hhid}?

















































{pstd}{cmd:iddup} can correctly give us the number of observations that are "surplus"
in the sense that they are the second or third or fourth copy
of the first observation of each group of duplicates.
In other words,
{cmd:iddup} indicates the number of observations
that {cmd:hhid} is from being unique.

{pstd}{bf:{stata `"count if iddup > 0"'}}{p_end}

{pstd}However, not all duplicates have {cmd:iddup == 1}:

{pstd}{bf:{stata `"duplicates tag hhid, gen(duptag)"'}}{p_end}

{pstd}{bf:{stata `"list hhid iddup duptag if iddup > 0 | duptag > 0"'}}{p_end}

{pstd}One observation of each duplicate {cmd:hhid} (the first observation)
has {cmd:iddup == 0}.
To fix this, we have to modify the logical expression for {cmd:iddup}:

{pstd}{bf:{stata `"drop iddup"'}}{p_end}

{pstd}{bf:{stata `"sort hhid"'}}{p_end}
{pstd}{bf:{stata `"generate iddup = hhid == hhid[_n - 1] | hhid == hhid[_n + 1]"'}}{p_end}

{pstd}{bf:{stata `"count if iddup > 0"'}}{p_end}

{pstd}{bf:{stata `"list hhid iddup duptag if iddup > 0 | duptag > 0"'}}{p_end}

{pstd}When using {cmd:_n},
the sort order is paramount.
Before creating {cmd:iddup}, we sorted by {cmd:hhid},
but suppose we had sorted by {cmd:sex} instead:

{pstd}{bf:{stata `"sort sex"'}}{p_end}
{pstd}{bf:{stata `"generate iddup2 = hhid == hhid[_n - 1] | hhid == hhid[_n + 1]"'}}{p_end}

{pstd}{bf:{stata `"count if iddup  > 0"':count if iddup{space 2}> 0}}{p_end}
{pstd}{bf:{stata `"count if iddup2 > 0"'}}{p_end}

{hline}{marker q4}

{pstd}{bf:Problem Set Question 4}

{hline}

{pstd}{bf:{stata `"use "Raw/Stata 103", clear"'}}{p_end}
{pstd}{txt}{sf}{ul off}{...}

{pstd}{bf:(a)}

{pstd}There are {cmd:1001} observations in the dataset.
Use {cmd:_n} to create a variable of consecutive integers from {cmd:1} to {cmd:1001},
named {cmd:n}.
{txt}{sf}{ul off}{...}

{pstd}{bf:(b)}

{pstd}Using {helpb sum()}, create a variable named {cmd:rsum} that is the running sum of {cmd:n}.
{txt}{sf}{ul off}{...}

{pstd}{bf:(c)}

{pstd}Create a variable named {cmd:triangular} that is equal to {cmd:n * (n + 1) / 2}.
{txt}{sf}{ul off}{...}

{pstd}{bf:(d)}

{pstd}For how many observations is {cmd:rsum == triangular}?
{txt}{sf}{ul off}{...}


{hline}

{pstd}Problem Set: {view `""SMCL/Problem sets/Concepts/Little n - problem set.smcl""':{bf:_n}}{break}

{pstd}Next: {view `""SMCL/Concepts/by.smcl""':{bf:by}}{break}
Previous: {view `""SMCL/Concepts/Big N.smcl""':{bf:_N}}{break}
{view `""Stata 103.smcl""':Stata 103 Start}{break}
