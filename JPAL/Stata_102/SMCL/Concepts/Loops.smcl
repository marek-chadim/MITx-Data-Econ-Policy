{smcl}
{txt}{...}
{pstd}{c TLC}{hline 43}{c TRC}{break}
{bf}{...}
{c |}{view `""Stata 102.smcl""': Innovations for Poverty Action{space 12}}{c |}{break}
{c |}{view `""Stata 102.smcl""': The Abdul Latif Jameel Poverty Action Lab{space 1}}{c |}{break}
{c |}{view `""Stata 102.smcl""':{space 43}}{c |}{break}
{c |}{view `""Stata 102.smcl""': Staff Training{space 28}}{c |}{break}
{c |}{view `""Stata 102.smcl""': Stata 102{space 33}}{c |}{break}
{sf}{...}
{c BLC}{hline 43}{c BRC}

{pstd}{bf:Loops}{break}

{pstd}{bf:{stata `"use "Raw/Stata 102", clear"'}}{p_end}

{pstd}{view `""SMCL/Concepts/Loops.smcl"##introduction"':1. Introduction}{break}
{view `""SMCL/Concepts/Loops.smcl"##foreach"':2. The Foreach Command}{break}
{view `""SMCL/Concepts/Loops.smcl"##writing"':3. Writing a Loop}{break}
{view `""SMCL/Concepts/Loops.smcl"##examples"':4. Loop Examples}{break}

{hline}{marker introduction}

{pstd}{bf:1. Introduction}

{hline}

{pstd}In Stata, loops are used to repeat a command multiple times or to execute
a series of commands that are related. There are several commands that work as loops;
{cmd:foreach} is by far the most widely used, so we will focus on it in this module.
Once you understand the syntax of one of the type of loops, you can learn any of them easily.

{hline}{marker foreach}

{pstd}{bf:2. The Foreach Command}

{hline}

{pstd}The most basic {cmd:foreach} syntax runs in the following way:

{pstd}{cmd}{...}
foreach {it:item} in list_item1 list_item2 list_item3 {{break}
{p_end}{pmore}command `item'{break}{p_end}{pstd}
}
{txt}{sf}{ul off}{...}

{pstd}where {it:item} is the {view `""SMCL/Concepts/Macros and Locals.smcl""':local macro} that is used to signify each item of the list.
So, as Stata loops through the command for each item on the list, it will
substitute each list_item into the macro, performing the following:

{pstd}{cmd}{...}
command list_item1{break}
command list_item2{break}
command list_item3
{txt}{sf}{ul off}{...}

{pstd}For example, execute the following loop:

{pstd}{cmd}foreach letter in a b c d {{txt}{p_end}
{pmore}{cmd}display "`letter'"{txt}{p_end}
{pstd}{cmd}}{txt}{p_end}

{pstd}{stata `"run "Do/Concepts/Loops.do" 1"':Click here to execute.}

{pstd}Does this output make sense? Here, the command is {cmd:display},
a b c d is the list of items we want to loop over, and {it:letter} is
the local macro we want to use to first declare and then call the list.

{pstd}{bf:Note:} the opening bracket { has to come at the end of
the line where the list is declared, and the closing bracket } comes
on a line by itself once the loop is complete.

{pstd}Why did Stata output these letters on separate lines? The {cmd:foreach}
command repeatedly sets the local macro `letter' to each element in the list
that follows {bf:in}, executing the commands in the loop for each value
of `letter'. {it:letter} is an arbitrary name, another could have been chosen
and the output of the loop would have been identical.

{marker whats_in_a_name}{...}
{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:Technical Tip!}}{c |}{p_end}
{pstd}{c LT}{hline 78}{c RT}{p_end}
{p2col:{c |} {bf:What's in a name?}}{c |}{p_end}
{pstd}{c LT}{hline 78}{c RT}{p_end}
{p2col:{c |} The same rules generally apply to all Stata names, whether it's of a}{c |}{p_end}
{p2col:{c |} variable, macro, or something else. From {helpb varname:help varname}:}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} "Variable names may be 1 to 32 characters long and must start with {cmd:a-z}, {cmd:A-Z},}{c |}{p_end}
{p2col:{c |} or {cmd:_}, and the remaining characters may be {cmd:a-z}, {cmd:A-Z}, {cmd:_}, or {cmd:0-9}."}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} The exception to this rule is local macros, which may be 1 to 31 characters}{c |}{p_end}
{p2col:{c |} long (a character shorter), but may start with {cmd:0-9}.}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}

{hline}{marker writing}

{pstd}{bf:3. Writing a Loop}

{hline}

{pstd}Imagine we want to write a loop that tabulates 3 variables in our dataset,
say, {bf:literateyn}, {bf:own4wheeleryn}, and {bf:theftfromcaryn}. How might
we go about doing so? We'll introduce here a handy way to go about
writing loops. This process is certainly not a must: the loop we're creating
here is relatively simple, and people write their loops in different ways,
nonetheless, it should prove useful.

{pstd}First write the command as it would be for the first variable in the loop.
In this case:

{pstd}{cmd}{...}
tabulate literateyn
{txt}{sf}{ul off}{...}

{pstd}Them, we write the loop around it:

{pstd}{cmd}{...}
foreach _ in ___ {{break}
{p_end}{pmore}tabulate literateyn{break}{p_end}{pstd}
}
{txt}{sf}{ul off}{...}

{pstd}Now, you should see where literateyn and the other list variables should
be placed:

{pstd}{cmd}{...}
foreach _ in literateyn own4wheeleryn theftfromcaryn {{break}
{p_end}{pmore}tabulate literateyn{break}{p_end}{pstd}
}
{txt}{sf}{ul off}{...}

{pstd}Finally, come up with a name for the local and insert it into the
{view `""SMCL/Concepts/Macros and Locals.smcl"##declaring"':declaration} and calling parts of the loop:

{pstd}{cmd}foreach var in literateyn own4wheeleryn theftfromcaryn {{txt}{p_end}
{pmore}{cmd}tabulate `var'{txt}{p_end}
{pstd}{cmd}}{txt}{p_end}

{pstd}{stata `"run "Do/Concepts/Loops.do" 2"':Click here to execute.}

{pstd}Remember the importance of putting single quotes around a local macro,
in this case, {bf:var}, when it is being {view `""SMCL/Concepts/Macros and Locals.smcl"##declaring"':called}.

{pstd}Also a quick note about style: notice that the code was indented on the line following
the opening brace while the closing brace stays aligned with {cmd:foreach}. This formatting
makes the code more readable; it's easy to tell what's in the loop and what's not.
This is an important practice to adopt, especially when using more complex constructions
such as loops within loops.

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:Technical Tip!}}{c |}{p_end}
{pstd}{c LT}{hline 78}{c RT}{p_end}
{p2col:{c |} For more tips on Stata programming style, see these resources, available on}{c |}{p_end}
{p2col:{c |} Box:}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} {browse "http://www.stata-journal.com/sjpdf.html?articlenum=pr0018":{it:Suggestions on Stata programming style}}}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} {browse "http://faculty.chicagobooth.edu/matthew.gentzkow/research/ra_manual_coding.pdf":{it:RA Manual: Notes on Writing Code}}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}

{hline}{marker examples}

{pstd}{bf:4. Loop Examples}

{hline}

{pstd}Let's look at a somewhat more complex example of using a loop to help us identify missing values in our
dataset. Execute the following loop:

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
foreach var in sex age educ {
{p_end}{pmore}display "Checking `var' for missing values..."{p_end}{pstd}
{p_end}{pmore}list hhid `var' if `var' == .{p_end}{pstd}
}
{txt}{sf}{ul off}{...}

{pstd}Here we loop over the variables {cmd:sex}, {cmd:age}, and {cmd:educ}.
In this dataset, these variables should never have missing values.
For each variable, the loop first displays the variable name as part of a message
({cmd:"Checking `var' for missing values..."}).
It then lists all values of {cmd:hhid} for which the variable is missing.
Knowing these, we can then examine the problematic observations more closely.

{pstd}The first element in the list is {cmd:"sex"}.
Thus, the local macro {cmd:`var'} is first set to {cmd:"sex"}.
The commands in the loop are then executed:

{pstd}{cmd}{...}
display "Checking {ul:`var'} for missing values..."{break}
list hhid {ul:`var'} if {ul:`var'} == .
{txt}{sf}{ul off}{...}

{pstd}The value of {cmd:`var'} is immediately substituted for {cmd:`var'} in the commands,
so this is the same as:

{pstd}{cmd}{...}
display "Checking {ul:sex} for missing values..."{break}
list hhid {ul:sex} if {ul:sex} == .
{txt}{sf}{ul off}{...}

{pstd}After {cmd:list}, all the commands in the for-loop have been executed,
so {cmd:`var'} is set to the next element in the list: {cmd:"age"} and then finally {cmd:"educ".}

{pstd}Also note that the contents of a list when using {cmd:foreach...in} do not have to be of
any specific variable {view `""SMCL/Concepts/Variable Types.smcl""':type}; they can be letters, variables, strings
or numeric, assuming the command you're using works with it. Again returning to the {cmd:display}
command:

{pstd}{cmd}foreach i in 1 2 3 purple cow "purple cow" {{txt}{p_end}
{pmore}{cmd}display "`i'"{txt}{p_end}
{pstd}{cmd}}{txt}{p_end}

{pstd}{stata `"run "Do/Concepts/Loops.do" 3"':Click here to execute.}

{pstd}In this module, we have only discussed one particular variation of the {cmd:foreach} command,
specifically {cmd:foreach...in}. In {bf: Stata 103} we'll discuss using
{cmd: of varlist} and {cmd: of numlist} along with {cmd:foreach} for added functionality.
As always, feel free to check out the {cmd:foreach} {help foreach:help file} to learn more
about the command.


{hline}

{pstd}Problem Set: {view `""SMCL/Problem sets/Concepts/Loops - problem set.smcl""':Loops}{break}

{pstd}Next: {view `""SMCL/Tasks/Importing.smcl""':Importing}{break}
Previous: {view `""SMCL/Concepts/Macros and Locals.smcl""':Macros and Locals}{break}
{view `""Stata 102.smcl""':Stata 102 Start}{break}
