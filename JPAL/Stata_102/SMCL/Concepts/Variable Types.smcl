{smcl}
{txt}{...}
{pstd}{c TLC}{hline 43}{c TRC}{break}
{bf}{...}
{c |}{view `""Stata 102.smcl""': Innovations for Poverty Action{space 12}}{c |}{break}
{c |}{view `""Stata 102.smcl""': The Abdul Latif Jameel Poverty Action Lab{space 1}}{c |}{break}
{c |}{view `""Stata 102.smcl""':{space 43}}{c |}{break}
{c |}{view `""Stata 102.smcl""': Staff Training{space 28}}{c |}{break}
{c |}{view `""Stata 102.smcl""': Stata 102{space 33}}{c |}{break}
{sf}{...}
{c BLC}{hline 43}{c BRC}

{pstd}{bf:Variable Types}{break}

{pstd}{bf:{stata `"use "Raw/Stata 102", clear"'}}{p_end}

{pstd}{view `""SMCL/Concepts/Variable Types.smcl"##strings"':1. Strings and Numerics}{break}
{view `""SMCL/Concepts/Variable Types.smcl"##destring"':2. Destring and Tostring}{break}
{view `""SMCL/Concepts/Variable Types.smcl"##encode"':3. Encode and Decode}{break}

{hline}{marker strings}

{pstd}{bf:1. Strings and Numerics}

{hline}

{pstd}There are two types of variables within Stata: {bf:string} and {bf:numeric}.

{pstd}{bf: What are strings?} A string is any data that is stored in Stata as text.
Numbers, letters, and punctuation can all be part of a string. Examples of strings include
"What do you get what you multiply six by seven?", "42", "m&m", and "%".

{pstd}Stata thinks of numbers differently whether they are strings or numerics. If a number is stored
as a string, Stata recognizes it not as a value, but as text, and would be treated in the same way
letters would be. So if you added the string "4" to the string "2", you would get "42" and not "6".

{pstd}{bf: What are numerics?} Numerics can only contain numbers, no letters, symbols or punctuation
(except periods). Numbers in numerical variables are recognized as values, and can be manipulated as such.
So, if you added numeric "4" and "2", you would get 6.

{pstd}For example, a variable that has values (1, 2, 5, 6.5, 7, 99) is numeric.
A variable that contains respondent or village names, or the "other, specify" type of variables
(meaning variables where people have to write in their own answer) are string variables.

{pstd}{bf: Note:} If a variable contains even one non-numeric character it is considered to be a string.
This may well be the result of a data entry error, say, specifying 'S' when the value was
supposed to be '5'. For example, a variable that has values (1, 0, O, 2, 5) would be a string,
and not numeric, as was mostly likely intended. We'll see in the next {view `""SMCL/Concepts/Variable Types.smcl"##destring"':section}
how to deal with such cases.

{pstd}There are a few methods to check whether the variable is string or numeric.
You can {cmd:describe} the variable or use the {cmd:codebook} command and verify the variable's
storage type. You can also simply look at the "Type" column in your variables window.
Any string will have str__ as its type (like str4, str11, str23), where the number after "str" (4, 11, 23)
is the number of characters of the longest value of the variable.
All other types are the different subsets of numeric variables (e.g. byte, float, etc.)
You can learn more about each subset through the data types {helpb datatypes:help file}.

{marker quotesmissing}{...}
{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:Technical Tip!}}{c |}{p_end}
{pstd}{c LT}{hline 78}{c RT}{p_end}
{p2col:{c |} {bf:Quotation Marks and Missing Values}}{c |}{p_end}
{pstd}{c LT}{hline 78}{c RT}{p_end}
{p2col:{c |} One important thing to remember about strings is that they need to be}{c |}{p_end}
{p2col:{c |} enclosed in quotation marks, since they can contain spaces, commas,}{c |}{p_end}
{p2col:{c |} and other characters that would otherwise confuse Stata.}{c |}{p_end}
{p2col:{c |} When using the {cmd:display} comamnd with numerics you would execute:}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} {bf:{stata display 4 + 2}}}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} However, when displaying a string, or numeric looking text, you}{c |}{p_end}
{p2col:{c |} must use quotation marks. See:}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} {bf:{stata display "this phrase"}}}{c |}{p_end}
{p2col:{c |} {bf:{stata display "4 + 2"}}}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} Missing values are also identified differently. For strings, missing}{c |}{p_end}
{p2col:{c |} values are designated as two quotation marks without a space in between: ""}{c |}{p_end}
{p2col:{c |} For numerics, the equivalent designation is the period/dot: .}{c |}{p_end}
{p2col:{c |} Don't worry if you don't remember these distinctions right now,}{c |}{p_end}
{p2col:{c |} we'll return to these concepts throughout the trainings}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}

{hline}{marker destring}

{pstd}{bf:2. Destring and Tostring}

{hline}

{pstd}The {cmd:destring} command converts string variables into numeric variables
while {cmd:tostring} makes numerics into strings.

{pstd}These commands are very useful, since when you import data, Stata might create variables with an incorrect type.
As we saw earlier, this can happen with a numeric variable being recorded as a string
when there's some extraenous character in the numeric by mistake. Imagine an income
variable erroneously containing the value '9o' rather than 90. This would make the variable
a string, which would subsequently impede any numerical analysis of the variable. To see this in practice,
try executing the {cmd:summarize} command on a string variable. However, imagine in
the cleaning process you catch this mistake and fix it, and now want to make the variable numeric to work with it.

{pstd}This is where {helpb destring:destring} comes in. The syntax is pretty straightforward, but note that the option
{bf:generate} (to generate a new variable name) or {bf:replace} (to replace the existing variable with the new one) must
be specified. When you first start off using these commands, it's recommended to use the {bf:generate}
option, so you can keep both variables and compare them to ensure the command worked as intended.

{pstd}{helpb tostring:tostring} uses the same syntax. Let's use both commands to get a sense of how they work. Observe that
the {bf:hhid} variable is a string, but imagine we want to convert it into a numeric. (Note: there is good reason why the
unique ID should be kept in string format, but let's change it just for the purposes of this exercise.)

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
destring hhid, generate(hhid_num)
{txt}{sf}{ul off}{...}

{pstd}We have now created a numeric version of {bf:hhid} called {bf:hhid_num}. Let's {cmd:browse}, {cmd:describe} and {cmd:summarize}
both variables.

{pstd}{bf:{stata `"browse hhid hhid_num"'}}{p_end}

{pstd}{bf:{stata `"describe hhid_num"'}}{p_end}
{pstd}{bf:{stata `"summarize hhid_num"'}}{p_end}

{pstd}{bf:{stata `"describe hhid"'}}{p_end}
{pstd}{bf:{stata `"summarize hhid"'}}{p_end}

{pstd}{bf:{stata `"drop hhid_num"'}}{p_end}

{pstd}Notice the differences in color when you {cmd:browse} and the differing output between both {cmd:summarize} commands.

{pstd}Let's now use {cmd:tostring} to convert a numeric to a string variable:

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
tostring literateyn, generate(literateyn_str)
{txt}{sf}{ul off}{...}

{pstd}We've created the new variable {bf:literateyn_str}. Let's take a look:

{pstd}{bf:{stata `"browse literateyn literateyn_str"'}}{p_end}
{pstd}{bf:{stata `"tabulate literateyn"'}}{p_end}
{pstd}{bf:{stata `"tabulate literateyn_str"'}}{p_end}

{pstd}{bf:{stata `"drop literateyn_str"'}}{p_end}

{pstd}What's going on here? Unlike {cmd:summarize}, the {cmd:tabulate} command works perfectly well
for both string and numeric variables. However, when we created the string variable, the
{view `""SMCL/Tasks/Naming and Labeling Variables.smcl"##vallabels"':value labels} were dropped, so tabulate just displays the
underlying values. In addition, notice how the number of observations between the two variables is different.
Why did this occur?

{...}
{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:Technical Tip!}}{c |}{p_end}
{pstd}{c LT}{hline 78}{c RT}{p_end}
{p2col:{c |} Sometimes {cmd:tostring} will fail to convert a variable to string, delivering the}{c |}{p_end}
{p2col:{c |} warning "cannot be converted reversibly." For example:}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} {bf:{stata sysuse auto, clear}}}{c |}{p_end}
{p2col:{c |} {bf:{stata tostring gear_ratio, replace}}}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} The solution to this is not option {cmd:force}, which results in loss of}{c |}{p_end}
{p2col:{c |} information. Instead, use option {cmd:format(%24.0g)}:}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} {bf:{stata tostring gear_ratio, replace format(%24.0g)}}}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} Option {cmd:format(%24.0g)} works not just in this case to resolve the issue, but}{c |}{p_end}
{p2col:{c |} in all cases.}{c |}{p_end}
{p2col:{c |} }{c |}{p_end}
{p2col:{c |} {bf:{stata use "Raw/Stata 102", clear}}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}

{hline}{marker encode}

{pstd}{bf:3. Encode and Decode}

{hline}

{pstd}As we have seen, {cmd:destring} and {cmd:tostring} offer a relatively simple way of converting
from a string variable to a numeric one and vice versa. One major disadvantage is that
{cmd:tostring} does not preserve any value labels associated with the variable.
Without the {cmd:force} or {cmd:ignore()} options, {cmd:destring} cannot
be used with a variable unless it is composed solely of numbers. The solution to these issues is to use
{cmd:encode} and {cmd:decode}. You can convert a string variable to a value labeled numeric variable
using {helpb encode:encode}:

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
encode thanavisitreason, generate(visitreason)
{txt}{sf}{ul off}{...}

{pstd}{bf:{stata `"browse thanavisitreason visitreason"'}}{p_end}

{pstd}Each unique value of the variable was assigned a number, with each value label corresponding to the strings
of the original variable. However, it might make sense to define and apply one's own value label:

{pstd}{cmd}label define visitreasonlab ///{txt}{p_end}
{pmore}{cmd}1{space 2}"To register a Crime" ///{txt}{p_end}
{pmore}{cmd}2{space 2}"To answer charges filed against you" ///{txt}{p_end}
{pmore}{cmd}3{space 2}"To say hello/to chat" ///{txt}{p_end}
{pmore}{cmd}97 "Refuse to answer" ///{txt}{p_end}
{pmore}{cmd}98 "Other" ///{txt}{p_end}
{pmore}{cmd}99 "Don't Know"{txt}{p_end}

{pstd}{stata `"run "Do/Concepts/Variable Types.do" 1"':Click here to execute.}

{...}
{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
encode thanavisitreason, generate(visitreason2) label(visitreasonlab)
{txt}{sf}{ul off}{...}

{pstd}{bf:{stata `"browse thanavisitreason visitreason visitreason2 if visitreason != visitreason2"'}}{p_end}

{pstd}You can also convert a value labeled variable
to a string variable that contains the value label text
using {helpb decode}:

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
decode visitreason, generate(visitreasonstr)
{txt}{sf}{ul off}{...}

{pstd}{bf:{stata `"browse thanavisitreason visitreason visitreasonstr"'}}{p_end}

{pstd}Just like {cmd:destring} and {cmd:tostring}, {cmd:encode} and {cmd:decode}
are inverse operations.

{pstd}If a string variable has all numeric values, convert it to numeric using {cmd:destring}.
If the variable does not have all numeric values,
use {cmd:encode} or use {cmd:destring} with option {cmd:ignore()} or {cmd:force}.

{pstd}If a numeric variable is not value labeled, convert it to string using {cmd:tostring}.
If the variable is value labeled,
you can use {cmd:decode} to retain the value label text or
{cmd:tostring} for just the numeric values.

{pstd}For more on the history of these sets of commands, see this interesting Stata
{browse "http://www.stata.com/support/faqs/data-management/destring-command/":FAQ post}.


{hline}

{pstd}Next: {view `""SMCL/Tasks/Unique IDs and Duplicates.smcl""':Unique IDs and Duplicates}{break}
Previous: {view `""SMCL/Tasks/Naming and Labeling Variables.smcl""':Naming and Labeling Variables}{break}
{view `""Stata 102.smcl""':Stata 102 Start}{break}
