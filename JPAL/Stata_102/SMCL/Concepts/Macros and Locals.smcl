{smcl}
{txt}{...}
{pstd}{c TLC}{hline 43}{c TRC}{break}
{bf}{...}
{c |}{view `""Stata 102.smcl""': Innovations for Poverty Action{space 12}}{c |}{break}
{c |}{view `""Stata 102.smcl""': The Abdul Latif Jameel Poverty Action Lab{space 1}}{c |}{break}
{c |}{view `""Stata 102.smcl""':{space 43}}{c |}{break}
{c |}{view `""Stata 102.smcl""': Staff Training{space 28}}{c |}{break}
{c |}{view `""Stata 102.smcl""': Stata 102{space 33}}{c |}{break}
{sf}{...}
{c BLC}{hline 43}{c BRC}

{pstd}{bf:Macros and Locals}{break}

{pstd}{bf:{stata `"use "Raw/Stata 102", clear"'}}{p_end}

{pstd}{view `""SMCL/Concepts/Macros and Locals.smcl"##introduction"':1. Introduction}{break}
{view `""SMCL/Concepts/Macros and Locals.smcl"##declaring"':2. Declaring and Calling Macros}{break}
{view `""SMCL/Concepts/Macros and Locals.smcl"##examples"':3. Using Macros}{break}
{view `""SMCL/Concepts/Macros and Locals.smcl"##numerics"':4. Using Macros with Numerics}{break}

{hline}{marker introduction}

{pstd}{bf:1. Introduction}

{hline}

{pstd}Up until now, we have worked solely with {view `""SMCL/Concepts/Variable Types.smcl""':variables} in Stata.
A Stata dataset is simply a collection of variables, sets of values,
which can be manipulated and analyzed in a variety of ways. However,
when writing code, variables have a number of drawbacks. In this chapter,
we’ll introduce an essential Stata concept, the macro.
We’ll explore macros through one particular type, the local.

{pstd}What is a macro? A macro simply stores a single value, and if you need
to save a value for later, it's often more convenient to attach it to a
macro than to create an entire variable for it.{space 2}Macros also facilitate
more complex code in a way that variables don't -- we'll get to that.

{pstd}A macro is basically a symbol that you program Stata to read as something else;
for example, if you tell Stata that X means "Apples", every time Stata sees X
it knows that you really mean "Apples".

{hline}{marker declaring}

{pstd}{bf:2. Declaring and Calling Macros}

{hline}

{pstd}There are two steps to working with macros:

{pstd}{bf:1) Declare} or create a macro, i.e. tell Stata that from now on, whenever you say
X you mean "Apples". One way to declare a macro is as follows:

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
local awesome "ipa and jpal"
{txt}{sf}{ul off}{...}

{pstd}Where "awesome" is the {it:local} macro, and "ipa & jpal" is the text it will
stand for in the future. Note that the macro in this case only reads ipa & jpal,
and the quotation marks aren’t treated as a part of the local.
In the declaration, Stata simply reads them to mean that you are showing
it text and not a variable or numbers.

{pstd}{bf:2) Call} the macro you created, meaning you use the new macro to pull
up the symbolized text. Local macros are called as follows:

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
display "`awesome'"
{txt}{sf}{ul off}{...}

{pstd}As you can see, since you declared "awesome" to mean "ipa and jpal", every time
you call awesome, Stata will know you actually mean "ipa and jpal".

{pstd}{bf:Note} the characters ` and ' are used when calling a local macro (but not
when declaring it). Without these {bf:single quotes}, Stata will not recognize
the local as a local and will just treat it as text or as a variable. To see this,
let's execute the previous command without these single quotes:

{pstd}{bf:{stata `"display "awesome""'}}{p_end}

{pstd}Single quotes comprise a forward quote (`) which is located to the left of
the "1" key, and a normal quote mark (') found next to the "Enter" key.

{pstd}Also note that in this case, we need to enclose the local with {bf:double quotes} (" "),
since the text it calls up is a string and needs to be treated as such. What happens if
we execute the following?

{pstd}{bf:{stata `"display `awesome'"'}}{p_end}

{pstd}Why is the result an error? "ipa and jpal" is copied in the place of `awesome', and then
the command is executed. So far so good. However, what Stata ends up executing is

{pstd}{bf:{stata `"display ipa and jpal"'}}{p_end}

{pstd}This we know will result in an error, as if you want to use the {helpb display} command to display
{view `""SMCL/Concepts/Variable Types.smcl""':strings}, you need to enclose them in double quotes. However, there are many instances
where we might put one or more variables in a local and then call it, in which case, we don't
use double quotes. Here we declare a local named "variable" and then call it with the
{cmd:summarize} command.

{pstd}{bf:{stata `"use "Raw/Stata 102", clear"'}}{p_end}
{pstd}{bf:{stata `"local variable age"'}}{p_end}
{pstd}{bf:{stata `"summarize `variable'"'}}{p_end}

{pstd}We'll see more examples of using variables in locals in the next module on {view `""SMCL/Concepts/Loops.smcl""':loops}.

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:Technical Tip!}}{c |}{p_end}
{pstd}{c LT}{hline 78}{c RT}{p_end}
{p2col:{c |} {bf:Locals}}{c |}{p_end}
{pstd}{c LT}{hline 78}{c RT}{p_end}
{p2col:{c |} Remember that a local is a subset of the larger category of macros.}{c |}{p_end}
{p2col:{c |} There are additional types of macros that can be created, however, for now,}{c |}{p_end}
{p2col:{c |} just know that the key characteristic of a local macro is that it only}{c |}{p_end}
{p2col:{c |} exists within the do-file that it was created. If you attempt to call}{c |}{p_end}
{p2col:{c |} `awesome' or `variable' from another do-file, you'll find that it}{c |}{p_end}
{p2col:{c |} doesn't exist. We'll learn about different types of macros, namely}{c |}{p_end}
{p2col:{c |} globals, in {bf:Stata 103}.}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}

{hline}{marker examples}

{pstd}{bf:3. Using Macros}

{hline}

{pstd}Macros might not seem so handy right now. After all, why can’t you just type "ipa & jpal"
instead of bothering with the macro? However, macros are useful in a variety of ways.
One is dealing with lists of variables, particularly those you need to reuse.
For example, imagine you needed to summarize a whole bunch of variables repeatedly
across the file. Instead of typing them over and over again, you can create a macro for
the entire list of variables, and then input them into various commands.

{pstd}Control variables in regressions is a good exmaple. You can simply declare all
the controls in a macro and then refer to that in multiple regressions,
saving you loads of time. There are many other wonderful uses as well that are more advanced.
You will discover them all in time.

{pstd}For now let’s try to get a hang of the format.
Let’s declare "i" as a local for number 1, and then generate a variable called "one"
that is equal to 1 throughout by using our local.

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
local i 1{break}
display "`i'"{break}
generate one = `i'
{txt}{sf}{ul off}{...}

{pstd}There are many great things you can do with locals that will allow you to write neat code.
For example you could nest a local within a local. Execute the following commands, and see
if you can figure out what's happening:

{pstd}{bf:{stata `"local a a1 a2 a3"'}}{p_end}

{pstd}{bf:{stata `"local b b1 b2 b3"'}}{p_end}

{pstd}{bf:{stata `"local ab `a' `b'"'}}{p_end}

{pstd}Let's now display the contents of the three locals we've just declared:

{pstd}{bf:{stata `"display "`a'""'}}{p_end}

{pstd}{bf:{stata `"display "`b'""'}}{p_end}

{pstd}{bf:{stata `"display "`ab'""'}}{p_end}

{pstd}Does this make sense? The local `ab' was declared as a list comprising the contents of
local `a' along with the contents of local `b'. Note the first word following the {cmd:local}
command is the local name and all subsequent terms are the local's contents. Let's look at a
another example:

{pstd}{bf:{stata `"local aone "the first one""'}}{p_end}

{pstd}{bf:{stata `"local 1 "one""'}}{p_end}

{pstd}{bf:{stata `"display "`aone'""'}}{p_end}

{pstd}{bf:{stata `"display "`a`1''""'}}{p_end}

{pstd}This result might be initially confusing. In the final line, the local `a`1'' is called.
First, the embedded local (`1') is called and it is exanded into "one" as declared previously.
Now the local reads simmply `aone', which has already been declared as "the first one", and thus,
that text is what is displayed.

{hline}{marker numerics}

{pstd}{bf:4. Using Macros with Numerics}

{hline}

{pstd}It is important to note that locals don't have to be only strings, they can also be numeric.
When you set a local to a number, Stata recognizes it to be a value, not just text. This can be very
useful when writing code. As an example, execute the following commands:

{p2colset 5 84 0 0}{...}
{pstd}{c TLC}{hline 78}{c TRC}{p_end}
{p2col:{c |} {it:It's the first time!} {bf:Try it yourself.}}{c |}{p_end}
{pstd}{c BLC}{hline 78}{c BRC}{p_end}
{p2colreset}{...}
{pstd}{...}{cmd}
local one 1{break}
display 2 + `one'
{txt}{sf}{ul off}{...}

{pstd}We can use any combination of mathematical operators with numeric locals as well:

{pstd}{bf:{stata `"local i 8"'}}{p_end}
{pstd}{bf:{stata `"display (`i' * 3)/2"'}}{p_end}

{pstd}As you have been playing around with locals, you may have noticed one thing:
you never got an error stating that it was impossible to create a local or change
it because it was already defined. When you were learning the {cmd:generate} command,
these warnings abounded – so what’s going on here?

{pstd}The question goes back to this: how do you redefine or drop a local?
Well, it’s extremely easy – you just declare it again! Every time a local is declared,
it overwrites whatever text was attached to that local previously. So, you can write:

{pstd}{bf:{stata `"local i 1"'}}{p_end}
{pstd}{bf:{stata `"local i 2"'}}{p_end}
{pstd}{bf:{stata `"local i = `i' + 3"'}}{p_end}

{pstd}And you can continue in this vein, and Stata will keep re-defininng `i'. What is the value
of `i' right now?

{pstd}When you learn loops in the next {view `""SMCL/Concepts/Loops.smcl""':module} and get comfortable with them,
you will soon realize that this makes locals (especially numeric locals) much easier to use with loops.

{pstd}As a final note, notice how an equal sign was used above when we last declared `i'. Up until then, we had not used
any equal signs. The reason is that in this case we are evaluating an expression, not simply copy and pasting
a value or values into a local. To see what's going on here, compare:

{pstd}{bf:{stata `"local num = 1 * 2 + 3"'}}{p_end}
{pstd}{bf:{stata `"display "`num'""'}}{p_end}

{pstd}with

{pstd}{bf:{stata `"local num 1 * 2 + 3"'}}{p_end}
{pstd}{bf:{stata `"display "`num'""'}}{p_end}

{pstd}Why are the results different? In the first instance, the expression {bf: 1 * 2 + 3} is evaluated,
then is stored within the local {bf: `num'}. In the second instance, without the = operator, Stata doesn't
evaluate the expression but simply stores the string as-is in the local {bf:`num'}. In {bf:Stata 103} we'll see
more examples where using the{space 2}= operator along with locals can be especially useful.


{hline}

{pstd}Next: {view `""SMCL/Concepts/Loops.smcl""':Loops}{break}
Previous: {view `""SMCL/Tasks/Unique IDs and Duplicates.smcl""':Unique IDs and Duplicates}{break}
{view `""Stata 102.smcl""':Stata 102 Start}{break}
